/*
 * Copyright (c) 2016 Onegini B.V.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

allprojects {
  apply plugin: 'maven'
  repositories {
    mavenLocal()
    mavenCentral()
    if (project.hasProperty('artifactory_user') && project.hasProperty('artifactory_password')) {
      maven {
        url "https://repo.onegini.com/artifactory/public"
        credentials {
          username artifactory_user
          password artifactory_password
        }
      }
    } else {
      throw new InvalidUserDataException("You must configure the 'artifactory_user' and 'artifactory_password' properties in your project before you can " +
          "build it.")
    }
  }
}

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId "com.onegini.mobile.exampleapp"
    minSdkVersion 16
    targetSdkVersion 23
    versionCode 5
    versionName "2.2.0"
    multiDexEnabled true
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  dexOptions {
    javaMaxHeapSize "4g"
  }
  lintOptions {
    ignoreWarnings true
    abortOnError false
  }
}

dependencies {
  // Onegini SDK
  compile('com.onegini.mobile.sdk.android:onegini-sdk:6.02.00@aar') {
    transitive = true
    exclude module: 'fido-client'
    exclude module: 'samsungsds-ccom'
  }

  // Google Support
  compile 'com.android.support:appcompat-v7:23.3.0'
  compile 'com.android.support:cardview-v7:23.3.0'
  compile 'com.android.support:recyclerview-v7:23.3.0'

  // Google cloud messaging (GCM)
  compile 'com.google.android.gms:play-services-gcm:9.6.1'

  // Support for multidex
  compile 'com.android.support:multidex:1.0.1'
  // DI for views
  compile 'com.jakewharton:butterknife:7.0.1'
  // RxJava
  compile 'io.reactivex:rxandroid:1.1.0'

  // Samsung's FIDO client
  compile 'com.samsung.sds.fido.uaf.sdk:fido-client:1.4.1@aar'
  compile 'org.fidoalliance.uaf.client.common:samsungsds-ccom:1.4.1@aar'
  compile 'com.samsung.sds.fido.uaf.message:samsungsds-um:1.4.1'
  // Google Guava
  compile 'com.google.guava:guava:19.0'
}
